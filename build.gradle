plugins {
    id 'java'
    id 'maven-publish'
    id("com.github.monosoul.yadegrap") version "1.0.0"
}

group 'me.bluetree242'
version '1.0-ALPHA'

repositories {
    mavenCentral()
    jcenter()
}
dependencies {
    implementation("net.dv8tion:JDA:5.0.0-alpha.5")

    //lombok
    compileOnly 'org.projectlombok:lombok:1.18.22'
    annotationProcessor 'org.projectlombok:lombok:1.18.22'

    testCompileOnly 'org.projectlombok:lombok:1.18.22'
    testAnnotationProcessor 'org.projectlombok:lombok:1.18.22'
}

task packageJavadoc(type: Jar, dependsOn: 'javadoc') {
    from javadoc.destinationDir
    classifier = 'javadoc'
}
task packageSources(type: Jar, dependsOn: 'classes') {
    from sourceSets.main.allSource
    classifier = 'sources'
}
artifacts {
    archives packageJavadoc
    archives packageSources
}

javadoc {
    dependsOn delombok
    options.links 'https://ci.dv8tion.net/job/JDA/javadoc/', 'https://docs.oracle.com/javase/8/docs/api/'
    source = delombok
    if (JavaVersion.current() in JavaVersion.VERSION_11..JavaVersion.VERSION_12)
        options.addBooleanOption('-no-module-directories', true)
}

publishing {
    publications {
        "$project.name"(MavenPublication) {
            groupId rootProject.group
            artifactId project.name
            version project.version
            from components.java
            artifact(packageSources)
            artifact(packageJavadoc)
        }
    }
}
